plugins {
	id 'java-library'
	id "org.jetbrains.kotlin.jvm" version "1.6.20"
	id "com.palantir.git-version" version "0.14.0"
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(8)
	}
}

kotlin {
	jvmToolchain {
		languageVersion = JavaLanguageVersion.of(8)
	}
}

test {
	useJUnitPlatform()
	//jvmArgs["-Djava.awt.headless=true"]
	//environment.remove("DISPLAY")
}

repositories {
	mavenCentral()
}

dependencies {
	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
	testImplementation 'org.jetbrains.kotlin:kotlin-test'
	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

def genDir = "$buildDir/gen/"

task versionGen {
	def versionFile = "$genDir/version.txt"
	outputs.file(versionFile)
	inputs.property('version', rootProject.version)
	doLast {
		new File(versionFile).text = """$rootProject.version
"""
	}
}

tasks.named('jar') {
	dependsOn versionGen
	dependsOn test
	manifest {
		attributes('Implementation-Title': rootProject.name,
				'Implementation-Version': rootProject.version,
				'Main-Class': 'hevs.graphics.FunGraphics')
	}
	archiveBaseName.set(rootProject.name)
	archiveVersion.set(rootProject.version)

	// Add sources to the jar
	from('src/main/java') {
		include '**/*.java'
	}
	from('src/main/kotlin') {
		include '**/*.kt'
	}

	from("$genDir") {
		rename {
			String filename -> "res/generated/" + filename
		}
	}
}

version = gitVersion()

task runJar(type: JavaExec) {
	dependsOn jar
	mainClass = "-jar";
	args jar.archiveFile.get()
}

